rule "Astro: sunset"
when
    // Sunset (early)
    Channel "astro:sun:home:set#event" triggered START
then
    logWarn("astro", "Sun: set")

    // We are going to sleep soon

    // Close all blinds
    blinds_auto_cmd.sendCommand("close")
    // Close all curtains
    curtains_auto_cmd.sendCommand("close")
end

rule "Astro: civil dawn"
when
    // Civil Dawn (early)
    Channel "astro:sun:home:civilDawn#event" triggered START
then
    logWarn("astro", "Sun: civilDawn")

    // Morning

    // Open all blinds
    blinds_auto_cmd.sendCommand("open")
    // Open all curtains
    curtains_auto_cmd.sendCommand("open")
end

// Update astro items

rule "Astro: update Sun"
when
    Item astro_sun_el changed
then
    // Sun going low
    if (newState > 5.0 | °) {
        if (astro_sun_up_5.state != ON) astro_sun_up_5.sendCommand(ON)
    } else {
        if (astro_sun_up_5.state != OFF) astro_sun_up_5.sendCommand(OFF)
    }
    // Sun leaving
    if (newState > 0.0 | °) {
        if (astro_sun_up_0.state != ON) astro_sun_up_0.sendCommand(ON)
    } else {
        if (astro_sun_up_0.state != OFF) astro_sun_up_0.sendCommand(OFF)
    }
end
