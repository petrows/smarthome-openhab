
// Motion detect in KG
rule "KG Motion detect treppe"
when
    Item tr_motion_occupancy received update
then
    logInfo("light", "KG Motion: " + newState.toString)
    if (newState == OFF) {
        // If light is triggered manually, does not stop it
        if (tr_manual_sw.state == ON) {
            logWarn("light", "KG Motion treppe OFF ignored by manual override")
        } else {
            sendCommand(tr_light, OFF)
        }
        // Switch off decor ligts - only if brightness is 15
        if (Math::round(Float::parseFloat(ku_light_table_dim.state.toString)) == 15) {
            ku_light_table_sw.sendCommand(OFF)
        }
        if (Math::round(Float::parseFloat(eg_decoration_light_dim.state.toString)) == 15) {
            eg_decoration_light_sw.sendCommand(OFF)
        }
        return
    }
    if (newState == ON) {
        if (tr_light.state == OFF) {
            sendCommand(tr_light, ON)
            sendCommand(tr_light_dim, 1)
        }
        // At night -> turn on some EG
        if (astro_sun_up_5.state == OFF) {
            // If no light at flur or toilet or kitchen -> turn on some
            if (
                   ku_light.state == OFF
                && fl_light.state == OFF
                && bz_light.state == OFF
            ) {
                sendCommand(eg_light_auto, ON)
                sendCommand(eg_light_auto_dim, 15)
            }
        }
    }
end

// Motion detect in Lager Fl√ºr
rule "KG Motion detect Lager"
when
    Item kg_lager4_motion_occupancy received update
then
    logInfo("light", "KG Lager Motion: " + newState.toString)
    if (newState == OFF) {
        // If light is triggered manually, does not stop it
        if (kg_lager4_manual_sw.state == ON) {
            logWarn("light", "KG Motion lager OFF ignored by manual override")
            return
        }
        sendCommand(g_light_lager_auto, OFF)
    }
    if (newState == ON && g_light_lager_auto.state == OFF) {
        sendCommand(g_dim_lager_auto, 30)
    }
end

// Motion detect in cat door
rule "Cat door motion detect"
when
    Item kg_lager3_cat_door_motion_occupancy received update
then
    logInfo("motion", "KG Cat Door Motion: " + newState.toString)
    if (newState == ON) {
        // Send command to ZoneMinder to trigger the cat door
        // We are starting for 10 minutes, should be cut-off by device (see OFF rule)
        executeCommandLine("/openhab/conf/bin/zm-trigger", "1", "on+600", "openhab-motion")
    }
    if (newState == OFF) {
        executeCommandLine("/openhab/conf/bin/zm-trigger", "1", "off", "")
    }
end
