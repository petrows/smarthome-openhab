
// Motion detect in KG
rule "KG Motion detect treppe"
when
    Item treppe_motion_occupancy changed
then
    var state = treppe_motion_occupancy.state
    if (state == OFF) {
        // If light is triggered manually, does not stop it
        if (treppe_manual_sw.state == ON) {
            logWarn("light", "KG Motion treppe OFF ignored by manual override")
        } else {
            sendCommand(g_light_treppe, OFF)
        }
        sendCommand(g_light_eg_auto, OFF)
        return
    }
    if (astro_is_night.state == OFF) {
        logWarn("light", "KG Motion skipped at day")
        return
    }
    if (state == ON) {
        if (g_light_treppe.state == OFF) {
            sendCommand(g_dim_treppe, 30)
        }
        if (g_light_eg_auto.state == OFF) {
            sendCommand(g_dim_eg_auto, 15)
        }
    }
end

// Motion detect in Lager Fl√ºr
rule "KG Motion detect Lager"
when
    Item kg_lager4_motion_occupancy changed
then
    var state = kg_lager4_motion_occupancy.state
    if (state == OFF) {
        // If light is triggered manually, does not stop it
        if (kg_lager4_manual_sw.state == ON) {
            logWarn("light", "KG Motion lager OFF ignored by manual override")
            return
        }
        sendCommand(g_light_lager_auto, OFF)
    }
    if (astro_is_night.state == OFF) {
        logWarn("light", "KG Motion lager skipped at day")
        return
    }
    if (state == ON && g_light_lager_auto.state == OFF) {
        sendCommand(g_dim_lager_auto, 30)
    }
end
