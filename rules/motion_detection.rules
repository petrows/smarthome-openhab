
// Motion detect in KG
rule "KG Motion detect treppe"
when
    Item treppe_motion_occupancy received command
then
    if (receivedCommand == OFF) {
        // If light is triggered manually, does not stop it
        if (treppe_manual_sw.state == ON) {
            logWarn("light", "KG Motion treppe OFF ignored by manual override")
        } else {
            sendCommand(g_light_treppe, OFF)
        }
        // Switch off decor ligts - only if brightness is 15
        if (Math::round(Float::parseFloat(ku_light_table_dim.state.toString)) == 15) {
            ku_light_table_sw.sendCommand(OFF)
        }
        if (Math::round(Float::parseFloat(eg_decoration_light_dim.state.toString)) == 15) {
            eg_decoration_light_sw.sendCommand(OFF)
        }
        return
    }
    if (receivedCommand == ON) {
        if (g_light_treppe.state == OFF) {
            sendCommand(g_light_treppe, ON)
            sendCommand(g_dim_treppe, 1)
        }
        // At night -> turn on some EG
        if (astro_sun_up_5.state == OFF) {
            // If no light at flur or toilet or kitchen -> turn on some
            if (
                   g_light_eg_ku.state == OFF
                && g_light_eg_flur.state == OFF
                && g_light_eg_bz.state == OFF
            ) {
                sendCommand(g_light_eg_auto, ON)
                sendCommand(g_dim_eg_auto, 15)
            }
        }
    }
end

// Motion detect in Lager Fl√ºr
rule "KG Motion detect Lager"
when
    Item kg_lager4_motion_occupancy received command
then
    if (receivedCommand == OFF) {
        // If light is triggered manually, does not stop it
        if (kg_lager4_manual_sw.state == ON) {
            logWarn("light", "KG Motion lager OFF ignored by manual override")
            return
        }
        sendCommand(g_light_lager_auto, OFF)
    }
    if (receivedCommand == ON && g_light_lager_auto.state == OFF) {
        sendCommand(g_dim_lager_auto, 30)
    }
end

// Motion detect in Laundry
rule "KG Motion detect Laundry"
when
    Item kg_lager3_motion_occupancy received command
then
    if (receivedCommand == OFF) {
        sendCommand(kg_lager3_main_light, OFF)
    } else {
        sendCommand(kg_lager3_main_light, ON)
    }
end
